(* 

Batch change the timestamp of multiple videos at once
When video files from old cameras are imported 
into Photos app, the date of the video gets set
to the imported time. Ideally we expect the time
to be taken from the exif or from the created or
modified time stamp. But this doesn't happen for 
some files.
This causes the video to be shown on the wrong 
time line. Taking each photo and adjusting the 
timestamp is difficult.
This script takes the timestamp from the filename
and sets it to date attribute which is used by
Photos app.
The script requires the filename to start with 
the following format
YYYYMMDD_HHMMSS.
If your current file name is not in this format,
please use some tool to change it to that format
first.
I use Advanced Renamer tool in Windows.
I usually rename all photos/videos from
all my photos and cameras in this format
for easy searching.

Some Ideas were borrowed from these links.
https://gist.github.com/RichardHyde/3386ac57b55455b71140
https://discussions.apple.com/docs/DOC-250000327

*)



(* select at least 1 images in Photos *)
tell application "Photos"
	activate
	set selectedVideos to (get selection)
	if (selectedVideos is {}) or (the length of selectedVideos < 1) then
		error "Please select at least one image."
	else
		
		repeat with i from 1 to count of selectedVideos
			
			set currentVideo to item i of selectedVideos
			set currentVideoDateTime to (the date of currentVideo) as date
			
			set currentVideoFileName to filename of currentVideo
			
			set modifiedDateTime to the current date
			set the month of modifiedDateTime to (1 as integer)
			set the day of modifiedDateTime to (1 as integer)
			
			set the year of modifiedDateTime to (text 1 thru 4 of currentVideoFileName)
			set the month of modifiedDateTime to (text 5 thru 6 of currentVideoFileName)
			set the day of modifiedDateTime to (text 7 thru 8 of currentVideoFileName)
			
			set the time of modifiedDateTime to 0
			
			set the hours of modifiedDateTime to (text 10 thru 11 of currentVideoFileName)
			set the minutes of modifiedDateTime to (text 12 thru 13 of currentVideoFileName)
			set the seconds of modifiedDateTime to (text 14 thru 15 of currentVideoFileName)
			
			
			tell currentVideo
				set the date of currentVideo to (modifiedDateTime) as date
			end tell
			
			
		end repeat
	end if
	return "Adjusted the date and time of " & (the length of selectedVideos) & " videos"
end tell

